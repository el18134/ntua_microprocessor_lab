		IN 10H		;Απενεργοποίηση προστασίας μνήμης
		MVI A,10H
		STA 0903H	;Βάζουμε κενά στα ψηφία του 7-segment display
		STA 0904H	;που δεν χρησιμοποιούμε
		STA 0905H

INPUT1:	CALL KIND	;Λαμβάνουμε είσοδο x από το πληκτρολόγιο
		CPI 00H	;Ελέγχουμε ότι η είσοδος είναι δεκαεξαδικό ψηφίο
		JC INPUT1
		CPI 10H
		JNC INPUT1
		RLC		;Κάνουμε 4 περιστροφές ώστε η είσοδος να πολλαπλασιαστεί με 16
		RLC
		RLC
		RLC	
		MOV B,A	;Αποθηκεύουμε το 16x στον Β
INPUT2:	CALL KIND	;Λαμβάνουμε είσοδο y από το πληκτρολόγιο
		CPI 00H	;Ελέγχουμε ότι η είσοδος είναι δεκαεξαδικό ψηφίο
		JC INPUT2
		CPI 10H
		JNC INPUT2
		ADD B		;Προσθέτουμε το 16x στο y
		MOV B,A	;Αποθηκεύουμε τον τελικό αριθμό στον Α

		MVI D,10H
		CPI 64H	;Ελέγχουμε αν ο αριθμός είναι μεγαλύτερος του 99D
		JC TENS
		MVI D,00H	
HUNDREDS:	SUI 64H	;Αν είναι αφαιρούμε 100 από αυτόν μέχρι να μην είναι
		INR D		;Μετράμε πόσες εκατοντάδες περιέχει ο αριθμός
		CPI 64H
		JC TENS	;Αν είναι μικρότερος του 100 ελέγχουμε τις δεκάδες
		JMP HUNDREDS

TENS:		MOV B,A
		MOV A,D
		STA 0902H	;Αποθηκεύουμε τις εκατοντάδες στην κατάλληλη θέση μνήμης
		MOV A,B
		MVI D,00H
		CPI 0AH	;Ελέγχουμε αν ο αριθμός είναι μεγαλύτερος του 9D
		JC ONES
CONT:		SUI 0AH	;Αν είναι αφαιρούμε 10 από αυτόν μέχρι να μην είναι
		INR D		;Μετράμε πόσες δεκάδες περιέχει ο αριθμός
		CPI 0AH
		JC ONES	;Αν είναι μικρότερος του 100 ελέγχουμε τις μονάδες
		JMP CONT

ONES:		MOV B,A
		MOV A,D
		STA 0901H	;Αποθηκεύουμε τις δεκάδες στην κατάλληλη θέση μνήμης
		MOV A,B	;Ο αριθμός που παραμένει στον Α είναι οι μονάδες
		STA 0900H	;Αποθηκεύουμε τις μονάδες στην κατάλληλη θέση μνήμης
		
		LDA 0902H
		CPI 10H
		JNZ PRINT
		LDA 0901H	;Ελέγχουμε τις εκατοντάδες και τις δεκάδες και βάζουμε κενά όπου χρειάζεται
		CPI 00H	
		JNZ PRINT	
		MVI A,10H
		STA 0901H

PRINT:		CALL SCREEN	;Καλούμε την ρουτίνα απεικόνισης στο 7-segment display
		JMP INPUT1	;Επανακινούμε το πρόγραμμα

SCREEN:	PUSH D		;Ρουτίνα απεικόνισης στο 7-segment display
		LXI D,0900H	;Αρχή διευθύνσεων αποθήκευσης για το 7-segment display
		CALL STDM
		CALL DCD	
		POP D
		RET

		END