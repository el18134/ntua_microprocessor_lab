		LXI B,03E8H	;Αρχικοποίηση χρονοκαθυστέρησης σε 1000ms=1sec

START:		IN 20H		;Διάβασμα εισόδου
		RAL		;Ολίσθηση και έλεγχος του MSB
		JNC START	;Επανάληψη ελέγχου όσο το MSB είναι ίσο με 0
		RAR
		ANI 0FH	;Επαναφορά της εισόδου και απομόνωση των τεσσάρων LSB 
		MOV D,A	;Αποθήκευση του αριθμού στον D και άυξηση κατά 1
		INR D
		MVI A,00H	;Μηδενισμός του Α

CNTUP:		CMA		
		OUT 30H	;Συμπλήρωμα ως προς το 1 και απεικόνιση χρόνου σε δυαδική μορφή
		CALL DELB	
		CMA		
		INR A		;Αύξηση του Α κατά 1
		CALL STATIC	;Κλήση ρουτίνας ελέγχου λειτουργίας
		CMP D		;Έλεγχος εάν ο Α είναι ίσος με την είσοδο
		JC CNTUP	;Αν όχι συνεχίζεται η μέτρηση προς τα πάνω αλλιώς ξεκινά η αντίστροφη μέτρηση

		DCR A		;Μείωση του Α κατά δύο ώστε να ισούται με την είσοδο-1
		DCR A
CNTDN:		CMA		
		OUT 30H	;Συμπλήρωμα ως προς το 1 και απεικόνιση χρόνου σε δυαδική μορφή
		CALL DELB	
		CMA		
		DCR A		;Μείωση του Α κατά 1
		CALL STATIC	;Κλήση ρουτίνας ελέγχου λειτουργίας
		CPI 00H	;Έλεγχος εάν ο Α είναι ίσος με 0
		JNZ CNTDN	
		JMP START 	;Αν όχι συνεχίζεται η μέτρηση προς τα κάτω αλλιώς επανακίνηση του προγράμματος
		
STATIC:	PUSH PSW	;Ρουτίνα ελέγχου λειτουργίας προγράμματος (έλεγχος  MSB)
STOP:		IN 20H		;Διάβασμα εισόδου
		RAL		;Ολίσθηση και έλεγχος του MSB
		JNC STOP	;Επανάληψη ελέγχου όσο το MSB είναι ίσο με 0
		POP PSW
		RET
		
		END